#version 460
#extension GL_EXT_scalar_block_layout : require
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_16bit_storage : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "maaf_common.h"

layout(binding = 3, set = 0, scalar) readonly buffer worldPositionBuffer                { vec4 world_position[]; };
layout(binding = 4, set = 0, scalar) readonly buffer worldNormalBuffer                  { vec3 world_normal[]; };
layout(binding = 5, set = 0, scalar) readonly buffer useFilterBuffer                    { uint use_filter[]; };
layout(binding = 6, set = 0, scalar) readonly buffer MAAFParametersBuffer               { MAAFParametersGroup params[]; };
layout(binding = 7, set = 0, scalar) readonly buffer MAAFIntermediateDirectBuffer       { IntermediateStruct directIntermediate[]; };
layout(binding = 8, set = 0, scalar) readonly buffer MAAFIntermediateIndirectBuffer     { IntermediateStruct indirectIntermediate[]; };

layout(binding = 16, set = 0, scalar) writeonly buffer MAAFIntermediateDirectTmpBuffer  { IntermediateStruct directIntermediateTmp[]; };
layout(binding = 17, set = 0, scalar) writeonly buffer MAAFIntermediateIndirectTmpBuffer{ IntermediateStruct indirectIntermediateTmp[]; };

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

shared vec4 sWorldPosition[32 + 20];
shared vec3 sWorldNormal[32 + 20];
shared IntermediateStruct sDirectIntermediate[32 + 20];
shared IntermediateStruct sIndirtIntermediate[32 + 20];

void main()
{
    // Get current pixel position and global index.
    const uvec2 pixel = gl_GlobalInvocationID.xy;
    // else compute the gid
    const uint gid = pixel.x + pixel.y * resolution.x;

    if(gl_LocalInvocationID.x < 10) {
        const uint lid = max(gl_GlobalInvocationID.x - 10, 0) + pixel.y * resolution.x;
        sDirectIntermediate[gl_LocalInvocationID.x] = directIntermediate[lid];
        sIndirtIntermediate[gl_LocalInvocationID.x] = indirectIntermediate[lid];
        sWorldPosition[gl_LocalInvocationID.x] = world_position[lid];
        sWorldNormal[gl_LocalInvocationID.x] = world_normal[lid];
    }
    if(gl_LocalInvocationID.x >= 32-10) {
        const uint lid = min(gl_GlobalInvocationID.x + 10, resolution.x-1) + pixel.y * resolution.x;
        sDirectIntermediate[gl_LocalInvocationID.x + 20] = directIntermediate[lid];
        sIndirtIntermediate[gl_LocalInvocationID.x + 20] = indirectIntermediate[lid];
        sWorldPosition[gl_LocalInvocationID.x + 20] = world_position[lid];
        sWorldNormal[gl_LocalInvocationID.x + 20] = world_normal[lid];
    }
    const uint max_index = resolution.x - 1 + pixel.y * resolution.x;
    sWorldPosition[gl_LocalInvocationID.x + 10] = world_position[min(gid, max_index)];
    sWorldNormal[gl_LocalInvocationID.x + 10] = world_normal[min(gid, max_index)];
    const uint sid_0 = (gl_GlobalInvocationID.x - gl_LocalInvocationID.x) + pixel.y * resolution.x;
    for(int i=int(gl_LocalInvocationID.x); i<25*32; i+=32) {
        const int sid = i/25;
        const int eid = i%25;
        if(sid + gl_GlobalInvocationID.x - gl_LocalInvocationID.x >= resolution.x) break;
        sDirectIntermediate[sid+10].data[eid] = directIntermediate[sid+sid_0].data[eid];
        sIndirtIntermediate[sid+10].data[eid] = indirectIntermediate[sid+sid_0].data[eid];
    }
    barrier();

    // If the pixel is outside of the image, don't do anything:
    if((pixel.x >= resolution.x) || (pixel.y >= resolution.y))
        return;

    // return if do not use filter
    uint center_use_filter = use_filter[gid];
    if(center_use_filter==0) return;

    // TODO:: Further restrict the filter size by sigmaxp
    MAAFParametersGroup group = params[gid];
    const vec4 center_world_pos = world_position[gid];
    const vec3 center_world_norm = world_normal[gid];
    IntermediateStruct directInterm;
    IntermediateStruct indircInterm;
    
    clearIntermediateStruct(directInterm);
    clearIntermediateStruct(indircInterm);

    if(center_use_filter==2) {
        group.directParams.sigmaxp[0] *= use_AAF_para;
        group.indirectParams.sigmaxp[0] *= use_AAF_para;
    }

    // go through the whole filter
    bool on_focus_plane = false;
    int valid_filter_radius = min(min(int(pixel.x), filter_radius), int(resolution.x)-1-int(pixel.x));
    for (int i=int(pixel.x)-valid_filter_radius; i<=int(pixel.x)+valid_filter_radius; i++) {
        if (i < 0 || i >= resolution.x) continue;
        uint neighbor_gid = i + pixel.y*resolution.x;
        if(use_filter[neighbor_gid] == 0) continue;
        if(group.directParams.cxp[1] * params[neighbor_gid].directParams.cxp[1]<=0
        || params[neighbor_gid].directParams.scaling[0] == -1)
            on_focus_plane = true;

        // neighbor information fetching
        const vec4 neighbor_world_pos = sWorldPosition[neighbor_gid-sid_0 + 10];
        const vec3 neighbor_world_norm = sWorldNormal[neighbor_gid-sid_0 + 10];
        if(abs(dot(center_world_norm, neighbor_world_norm)) < cos_angle_threshold) continue;
        const vec3 diff = center_world_pos.xyz - neighbor_world_pos.xyz;
        const float euclidean_distancesq = dot(diff, diff);
        const float normcomp = dot(diff, light_normal);
        const float distancesq = euclidean_distancesq - normcomp*normcomp;
        // TODO:: Check whether use the condition (if has defocus blur)
        if(distancesq >= dist_threshold) continue;

        IntermediateStruct neighbor_directInterm = sDirectIntermediate[neighbor_gid-sid_0 + 10];
        IntermediateStruct neighbor_indirectInterm = sIndirtIntermediate[neighbor_gid-sid_0 + 10];
        // TODO:: Check whether each box is valid
        // TODO:: Use per box scaling
        float triweight[5];
        float weight[5];
        const float dist = sqrt(distancesq);
        const float pix_dist = float(i-int(pixel.x));
        const float pix_dist_sq = pix_dist*pix_dist;
        float scaling_1 = group.directParams.scaling[0];
        float scaling_2 = group.directParams.scaling[1];
        float weight_1;
        float weight_2;
        if(scaling_1!=-1 && scaling_2!=-1) {
            weight_1 = unnormalized_gaussian_sq(pix_dist_sq, group.directParams.sigmaxp[1]);
            weight_2 = unnormalized_gaussian_sq(pix_dist_sq, group.directParams.sigmaxp[2]);
            // TODO :: pix_dist*2*k_pi?
            triweight[0] = unnormalized_gaussian_sq(pix_dist_sq, group.directParams.sigmaxp[0]);
            triweight[1] = weight_1 * cos(group.directParams.cxp[1] * pix_dist) * scaling_1;
            triweight[2] = weight_2 * cos(group.directParams.cxp[2] * pix_dist) * scaling_2;
            triweight[3] = weight_1 * sin(group.directParams.cxp[1] * pix_dist);
            triweight[4] = weight_2 * sin(group.directParams.cxp[2] * pix_dist);
            weight[0] = triweight[0];
            weight[1] = weight_1;
            weight[2] = weight_2;
            weight[3] = weight_1;
            weight[4] = weight_2;
        }
        else {
            triweight[0] = unnormalized_gaussian_sq(pix_dist_sq, group.directParams.sigmaxp[0]);
            triweight[1] = 0;
            triweight[2] = 0;
            triweight[3] = 0;
            triweight[4] = 0;
            weight[0] = triweight[0];
            weight[1] = 0;
            weight[2] = 0;
            weight[3] = 0;
            weight[4] = 0;
        }
        for(int j=0; j<5; ++j)
            for(int k=0; k<5; ++k)
                directInterm.data[j*5+k] += neighbor_directInterm.data[j*5+k] * vec4(vec3(triweight[j]), weight[j]);

        scaling_1 = group.indirectParams.scaling[0];
        scaling_2 = group.indirectParams.scaling[1];
        if(scaling_1!=-1 && scaling_2!=-1) {
            weight_1 = unnormalized_gaussian_sq(pix_dist_sq, group.indirectParams.sigmaxp[1]);
            weight_2 = unnormalized_gaussian_sq(pix_dist_sq, group.indirectParams.sigmaxp[2]);
            triweight[0] = unnormalized_gaussian_sq(pix_dist_sq, group.indirectParams.sigmaxp[0]);
            triweight[1] = weight_1 * cos(group.indirectParams.cxp[1] * pix_dist) * scaling_1;
            triweight[2] = weight_2 * cos(group.indirectParams.cxp[2] * pix_dist) * scaling_2;
            triweight[3] = weight_1 * sin(group.indirectParams.cxp[1] * pix_dist);
            triweight[4] = weight_2 * sin(group.indirectParams.cxp[2] * pix_dist);
            weight[0] = triweight[0];
            weight[1] = weight_1;
            weight[2] = weight_2;
            weight[3] = weight_1;
            weight[4] = weight_2;
        }
        else {
            triweight[0] = unnormalized_gaussian_sq(pix_dist_sq, group.indirectParams.sigmaxp[0]);
            triweight[1] = 0;
            triweight[2] = 0;
            triweight[3] = 0;
            triweight[4] = 0;
            weight[0] = triweight[0];
            weight[1] = 0;
            weight[2] = 0;
            weight[3] = 0;
            weight[4] = 0;
        }

        for(int j=0; j<5; ++j)
            for(int k=0; k<5; ++k)
                indircInterm.data[j*5+k] += neighbor_indirectInterm.data[j*5+k] * vec4(vec3(triweight[j]), weight[j]);
    }

    float hq_terms[5] = {1,0,0,0,0};
    float sin_terms[5] = {1,1,1,0,0};
    if(on_focus_plane) {
        for(int j=0; j<5; ++j)
            for(int k=0; k<5; ++k) {
                directInterm.data[j*5+k] *= vec4(hq_terms[j])*vec4(hq_terms[k]);
                if(directInterm.data[j*5+k].w==0) directInterm.data[j*5+k].a = -1;
                indircInterm.data[j*5+k] *= vec4(hq_terms[j])*vec4(hq_terms[k]);
                if(indircInterm.data[j*5+k].w==0) indircInterm.data[j*5+k].a = -1;
            }
    }

    directIntermediateTmp[gid]   = directInterm;
    indirectIntermediateTmp[gid] = indircInterm;
    return;
}