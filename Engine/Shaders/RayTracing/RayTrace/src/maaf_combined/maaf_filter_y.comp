#version 460
#extension GL_EXT_scalar_block_layout : require
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_16bit_storage : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "maaf_common.h"

layout(binding = 3, set = 0, scalar) readonly buffer worldPositionBuffer    { vec4 world_position[]; };
layout(binding = 4, set = 0, scalar) readonly buffer worldNormalBuffer      { vec3 world_normal[]; };
layout(binding = 5, set = 0, scalar) readonly buffer useFilterBuffer        { uint use_filter[]; };
layout(binding = 6, set = 0, scalar) readonly buffer MAAFParametersBuffer               { MAAFParametersGroup params[]; };
layout(binding = 7, set = 0, scalar) writeonly buffer MAAFIntermediateDirectBuffer       { IntermediateStruct directIntermediate[]; };
layout(binding = 8, set = 0, scalar) writeonly buffer MAAFIntermediateIndirectBuffer     { IntermediateStruct indirectIntermediate[]; };

layout(binding = 16, set = 0, scalar) readonly buffer MAAFIntermediateDirectTmpBuffer      { IntermediateStruct directIntermediateTmp[]; };
layout(binding = 17, set = 0, scalar) readonly buffer MAAFIntermediateIndirectTmpBuffer    { IntermediateStruct indirectIntermediateTmp[]; };

layout(local_size_x = 1, local_size_y = 32, local_size_z = 1) in;

shared vec4 sWorldPosition[32 + 20];
shared vec3 sWorldNormal[32 + 20];
shared IntermediateStruct sDirectIntermediate[32 + 20];
shared IntermediateStruct sIndirtIntermediate[32 + 20];

void main()
{
    // Get current pixel position and global index.
    const uvec2 pixel = gl_GlobalInvocationID.xy;
    // else compute the gid
    const uint gid = pixel.x + pixel.y * resolution.x;

    if(gl_LocalInvocationID.y < 10) {
        const uint lid = pixel.x + max(gl_GlobalInvocationID.y - 10, 0) * resolution.x;
        sDirectIntermediate[gl_LocalInvocationID.y] = directIntermediateTmp[lid];
        sIndirtIntermediate[gl_LocalInvocationID.y] = indirectIntermediateTmp[lid];
        sWorldPosition[gl_LocalInvocationID.y] = world_position[lid];
        sWorldNormal[gl_LocalInvocationID.y] = world_normal[lid];
    }
    if(gl_LocalInvocationID.y >= 32-10) {
        const uint lid = pixel.x + min(gl_GlobalInvocationID.y + 10, resolution.y-1) * resolution.x;
        sDirectIntermediate[gl_LocalInvocationID.y + 20] = directIntermediateTmp[lid];
        sIndirtIntermediate[gl_LocalInvocationID.y + 20] = indirectIntermediateTmp[lid];
        sWorldPosition[gl_LocalInvocationID.y + 20] = world_position[lid];
        sWorldNormal[gl_LocalInvocationID.y + 20] = world_normal[lid];
    }
    const uint max_index = pixel.x + (resolution.y-1) * resolution.x;
    sDirectIntermediate[gl_LocalInvocationID.y + 10] = directIntermediateTmp[min(gid, max_index)];
    sIndirtIntermediate[gl_LocalInvocationID.y + 10] = indirectIntermediateTmp[min(gid, max_index)];
    sWorldPosition[gl_LocalInvocationID.y + 10] = world_position[min(gid, max_index)];
    sWorldNormal[gl_LocalInvocationID.y + 10] = world_normal[min(gid, max_index)];
    barrier();

    // If the pixel is outside of the image, don't do anything:
    if((pixel.x >= resolution.x) || (pixel.y >= resolution.y))
        return;

    // return if do not use filter
    uint center_use_filter = use_filter[gid];
    if(center_use_filter==0) return;

    // TODO:: Further restrict the filter size by sigmaxp
    const MAAFParametersGroup group = params[gid];
    const vec4  center_world_pos = world_position[gid];
    const vec3  center_world_norm = world_normal[gid];
    IntermediateStruct directInterm;
    IntermediateStruct indircInterm;

    clearIntermediateStruct(directInterm);
    clearIntermediateStruct(indircInterm);

    bool on_focus_plane = false;
    int valid_filter_radius = min(min(int(pixel.y), filter_radius), int(resolution.y)-1-int(pixel.y));
    for (int i=int(pixel.y)-valid_filter_radius; i<=int(pixel.y)+valid_filter_radius; i++) {
        if (i < 0 || i >= resolution.y) continue;
        uint neighbor_gid = pixel.x + i*resolution.x;
        const int sid = i-int(pixel.y)+10+int(gl_LocalInvocationID.y);
        if(use_filter[neighbor_gid] == 0) continue;
        if(group.directParams.cxp[1] * params[neighbor_gid].directParams.cxp[1]<=0
            || params[neighbor_gid].directParams.scaling[0] == -1
            || directIntermediateTmp[neighbor_gid].data[1].w == -1)
            on_focus_plane = true;
        // neighbor information fetching
        const vec4 neighbor_world_pos = sWorldPosition[sid];
        const vec3 neighbor_world_norm = sWorldNormal[sid];
        if(abs(dot(center_world_norm, neighbor_world_norm)) < cos_angle_threshold) continue;
        const vec3 diff = center_world_pos.xyz - neighbor_world_pos.xyz;
        const float euclidean_distancesq = dot(diff, diff);
        const float normcomp = dot(diff, light_normal);
        const float distancesq = euclidean_distancesq - normcomp*normcomp;
        // TODO:: Check whether use the condition (if has defocus blur)
        if(distancesq >= dist_threshold) continue;

        IntermediateStruct neighbor_directInterm = sDirectIntermediate[sid];
        IntermediateStruct neighbor_indirectInterm = sIndirtIntermediate[sid];
        // TODO:: Check whether each box is valid
        // TODO:: Use per box scaling
        float triweight[5];
        float weight[5];

        const float dist = sqrt(distancesq);
        const float pix_dist = float(i-int(pixel.y));
        const float pix_dist_sq = pix_dist*pix_dist;
        float scaling_1 = group.directParams.scaling[0];
        float scaling_2 = group.directParams.scaling[1];
        float weight_1;
        float weight_2;

        if(scaling_1!=-1 && scaling_2!=-1) {
            weight_1 = unnormalized_gaussian_sq(pix_dist_sq, group.directParams.sigmaxp[1]);
            weight_2 = unnormalized_gaussian_sq(pix_dist_sq, group.directParams.sigmaxp[2]);
            triweight[0] = unnormalized_gaussian_sq(pix_dist_sq, group.directParams.sigmaxp[0]);
            triweight[1] = weight_1 * cos(group.directParams.cxp[1] * pix_dist) * scaling_1;
            triweight[2] = weight_2 * cos(group.directParams.cxp[2] * pix_dist) * scaling_2;
            triweight[3] = weight_1 * sin(group.directParams.cxp[1] * pix_dist);
            triweight[4] = weight_2 * sin(group.directParams.cxp[2] * pix_dist);
            weight[0] = triweight[0];
            weight[1] = weight_1;
            weight[2] = weight_2;
            weight[3] = weight_1;
            weight[4] = weight_2;
        }
        else {
            triweight[0] = unnormalized_gaussian_sq(pix_dist_sq, group.directParams.sigmaxp[0]);
            triweight[1] = 0;
            triweight[2] = 0;
            triweight[3] = 0;
            triweight[4] = 0;
            weight[0] = triweight[0];
            weight[1] = 0;
            weight[2] = 0;
            weight[3] = 0;
            weight[4] = 0;
        }
        for(int i=0; i<5; ++i)
            for(int j=0; j<5; ++j)
                directInterm.data[i*5+j] += neighbor_directInterm.data[i*5+j] * vec4(vec3(triweight[j]), weight[j]);

        scaling_1 = group.indirectParams.scaling[0];
        scaling_2 = group.indirectParams.scaling[1];
        if(scaling_1!=-1 && scaling_2!=-1) {
            weight_1 = unnormalized_gaussian_sq(pix_dist_sq, group.indirectParams.sigmaxp[1]);
            weight_2 = unnormalized_gaussian_sq(pix_dist_sq, group.indirectParams.sigmaxp[2]);
            triweight[0] = unnormalized_gaussian_sq(pix_dist_sq, group.indirectParams.sigmaxp[0]);
            triweight[1] = weight_1 * cos(group.indirectParams.cxp[1] * pix_dist) * scaling_1;
            triweight[2] = weight_2 * cos(group.indirectParams.cxp[2] * pix_dist) * scaling_2;
            triweight[3] = weight_1 * sin(group.indirectParams.cxp[1] * pix_dist);
            triweight[4] = weight_2 * sin(group.indirectParams.cxp[2] * pix_dist);
            weight[0] = triweight[0];
            weight[1] = weight_1;
            weight[2] = weight_2;
            weight[3] = weight_1;
            weight[4] = weight_2;
        }
        else {
            triweight[0] = unnormalized_gaussian_sq(pix_dist_sq, group.indirectParams.sigmaxp[0]);
            triweight[1] = 0;
            triweight[2] = 0;
            triweight[3] = 0;
            triweight[4] = 0;
            weight[0] = triweight[0];
            weight[1] = 0;
            weight[2] = 0;
            weight[3] = 0;
            weight[4] = 0;
        }
        for(int i=0; i<5; ++i)
            for(int j=0; j<5; ++j)
                indircInterm.data[i*5+j] += neighbor_indirectInterm.data[i*5+j] * vec4(vec3(triweight[j]), weight[j]);
    }

    float hq_terms[5] = {1,0,0,0,0};
    float sin_terms[5] = {1,1,1,0,0};
    if(on_focus_plane) {
        for(int i=0; i<5; ++i)
            for(int j=0; j<5; ++j) {
                directInterm.data[i*5+j] *= vec4(hq_terms[i]) * vec4(hq_terms[j]);
                indircInterm.data[i*5+j] *= vec4(hq_terms[i]) * vec4(hq_terms[j]);
            }
    }

    directIntermediate[gid]   = directInterm;
    indirectIntermediate[gid] = indircInterm;
    return;
}